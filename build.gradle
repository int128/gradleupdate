buildscript {
    ext.gaeVersion = '1.9.17'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.google.appengine:gradle-appengine-plugin:$gaeVersion"
    }
}

plugins {
    id 'groovy'
    id 'war'
}
apply plugin: 'appengine'

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile 'com.github.groovy-wslite:groovy-wslite:1.1.2'
    compile 'org.gaelyk:gaelyk:2.1.2'

    compile "com.google.appengine:appengine-api-1.0-sdk:$gaeVersion"
    compile "com.google.appengine:appengine-api-labs:$gaeVersion"
    compile 'javax.servlet:servlet-api:2.5'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'org.gaelyk:gaelyk-spock:0.4'
    testCompile "com.google.appengine:appengine-api-stubs:$gaeVersion"
    testCompile "com.google.appengine:appengine-testing:$gaeVersion"
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

war {
    webAppDirName = file('build/assets')
}

appengine {
    disableUpdateCheck = true
    warDir = file('build/assets')
    jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8890']
    appcfg {
        oauth2 = true
    }
}

sourceSets.main.output.classesDir = "${appengine.warDir}/WEB-INF/classes"

task syncDependencies(type: Sync) {
    from configurations.runtime
    into "${appengine.warDir}/WEB-INF/lib"
}
tasks.appengineRun.dependsOn(tasks.syncDependencies)
